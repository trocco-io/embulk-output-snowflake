plugins {
    id 'com.jfrog.bintray' version '1.8.4'
    id "com.github.jruby-gradle.base" version "1.6.0"
    id "java"
    id "checkstyle"
}
import com.github.jrubygradle.JRubyExec
repositories {
    mavenCentral()
    jcenter()
    maven {
        url "https://dl.bintray.com/embulk-output-jdbc/maven"
    }
}
configurations {
    provided
    defaultJdbcDriver
}

version = "0.0.1"

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile  "org.embulk:embulk-core:0.9.23"
    provided "org.embulk:embulk-core:0.9.23"
    // compile "YOUR_JAR_DEPENDENCY_GROUP:YOUR_JAR_DEPENDENCY_MODULE:YOUR_JAR_DEPENDENCY_VERSION"
    compile  "org.embulk.output.jdbc:embulk-output-jdbc:0.8.7"
    compile group: 'net.snowflake', name: 'snowflake-jdbc', version: '3.12.8'

    defaultJdbcDriver "net.snowflake:snowflake-jdbc:3.12.8"

    testCompile "junit:junit:4.+"
}

task classpath(type: Copy, dependsOn: ["jar"]) {
    doFirst { file("classpath").deleteDir() }
    from (configurations.runtime - configurations.provided - configurations.defaultJdbcDriver + files(jar.archivePath))
    into "classpath"
}

task defaultJdbcDriver(type: Copy, dependsOn: ["jar"]) {
    doFirst { file('default_jdbc_driver').deleteDir() }
    from (configurations.defaultJdbcDriver)
    into 'default_jdbc_driver'
}

clean {
    delete "classpath"
    delete "default_jdbc_driver"
}

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}

task gem(type: JRubyExec, dependsOn: ["gemspec", "classpath", "defaultJdbcDriver"]) {
    jrubyArgs "-S"
    script "gem"
    scriptArgs "build", "${project.name}.gemspec"
    doLast { ant.move(file: "${project.name}-${project.version}.gem", todir: "pkg") }
}

task gemPush(type: JRubyExec, dependsOn: ["gem"]) {
    jrubyArgs "-S"
    script "gem"
    scriptArgs "push", "pkg/${project.name}-${project.version}.gem"
}

task "package"(dependsOn: ["gemspec", "classpath"]) {
    doLast {
        println "> Build succeeded."
        println "> You can run embulk with '-L ${file(".").absolutePath}' argument."
    }
}

task gemspec {
    ext.gemspecFile = file("${project.name}.gemspec")
    inputs.file "build.gradle"
    outputs.file gemspecFile
    doLast { gemspecFile.write($/
Gem::Specification.new do |spec|
  spec.name          = "${project.name}"
  spec.version       = "${project.version}"
  spec.authors       = ["giwa"]
  spec.summary       = %[Snowflake output plugin for Embulk]
  spec.description   = %[Dumps records to Snowflake.]
  spec.email         = ["ugw.gi.world@gmail.com"]
  spec.licenses      = ["MIT"]
  spec.homepage      = "https://github.com/trocco-io/embulk-output-snowflake"

  spec.files         = `git ls-files`.split("\n") + Dir["classpath/*.jar"]
  spec.test_files    = spec.files.grep(%r"^(test|spec)/")
  spec.require_paths = ["lib"]

  #spec.add_dependency 'YOUR_GEM_DEPENDENCY', ['~> YOUR_GEM_DEPENDENCY_VERSION']
  spec.add_development_dependency 'bundler', ['~> 1.0']
  spec.add_development_dependency 'rake', ['~> 12.0']
end
/$)
    }
}
clean { delete "${project.name}.gemspec" }
